buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.12"
    }
}

plugins {
    id 'org.springframework.boot' version "$springBootVersion"
}

apply plugin: 'org.springframework.boot'
apply plugin: 'com.google.protobuf'

group = 'com.eshop'
version = '1.0.0-SNAPSHOT'
description = 'basket api'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8


tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

bootJar {
    archiveBaseName.set('basket-api')
    archiveVersion.set('1.0.0-SNAPSHOT')
}

dependencies {
    api('org.springframework.boot:spring-boot-starter-web')
    api('org.springframework.boot:spring-boot-starter-data-redis')
    api('redis.clients:jedis')
    api('io.grpc:grpc-netty')
    api('io.grpc:grpc-protobuf')
    api('io.grpc:grpc-stub')
    api('io.grpc:grpc-services')
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

sourceSets {
    main {
        proto {
            srcDir 'src/main/proto'
        }
        java {
            // include self written and generated code
            srcDirs 'src/main/java', 'generated-sources/main/java', 'generated-sources/main/grpc'
        }
    }
}

protobuf {
    // Configure the protoc executable
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:3.0.2'
    }

    // Configure the codegen plugins
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.1.2'
        }
    }

    generateProtoTasks.generatedFilesBaseDir = 'generated-sources'

    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}
